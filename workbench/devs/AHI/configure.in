dnl $Id$


dnl Process this file with autoconf to produce a configure script.

AC_REVISION( $Revision$ )
dnl
AC_INIT(TODO)


dnl General

AC_CANONICAL_SYSTEM
AC_PROG_MAKE_SET
AC_CONFIG_HEADER( config.h )

dnl Checks for programs.

AC_CHECK_TOOL( CC, gcc, : ) 
AC_CHECK_TOOL( AS, as, : ) 
AC_CHECK_TOOL( AR, ar, : )
AC_CHECK_TOOL( RANLIB, ranlib, : )
AC_CHECK_TOOL( STRIP, strip, : )
AC_CHECK_TOOL( OBJCOPY, objcopy, : ) 

AC_MSG_CHECKING(CPU to build for)
AC_ARG_WITH(cpu,
[  --with-cpu              The CPU to optimize for],
CPU=${withval}, unset CPU)
AC_MSG_RESULT(${CPU})

AC_MSG_CHECKING(OS include directory)
AC_ARG_WITH(os-includedir,
[  --with-os-includedir    Path to AmigaOS include directory],
os_includedir="-I${withval}",
os_includedir="")
AC_MSG_RESULT(${os_includedir} )

AC_MSG_CHECKING(if WarpUp support code should be enabled)
AC_ARG_ENABLE(warpup,
[  --enable-warpup         Enable WarpUp support],
AC_DEFINE(ENABLE_WARPUP))
ENABLE_WARPUP=${enable_warpup}
AC_SUBST(ENABLE_WARPUP)
AC_MSG_RESULT(${enable_warpup})

AC_MSG_CHECKING(Target CFLAGS)
AC_ARG_WITH(target-cflags,
[  --with-target-cflags    CFLAGS needed for the target],
target_cflags="${withval}",
target_cflags="")
AC_MSG_RESULT(${target_cflags} )

AC_MSG_CHECKING(Target CPPFLAGS)
AC_ARG_WITH(target-cppflags,
[  --with-target-cppflags    CPPFLAGS needed for the target],
target_cppflags="${withval}",
target_cppflags="")
AC_MSG_RESULT(${target_cppflags} )

AC_MSG_CHECKING(Target LDFLAGS)
AC_ARG_WITH(target-ldflags,
[  --with-target-ldflags    LDFLAGS needed for the target],
target_ldflags="${withval}",
target_ldflags="")
AC_MSG_RESULT(${target_ldflags} )

AC_MSG_CHECKING(Target ASFLAGS)
AC_ARG_WITH(target-asflags,
[  --with-target-asflags    ASFLAGS needed for the target],
target_asflags="${withval}",
target_asflags="")
AC_MSG_RESULT(${target_asflags} )

AC_MSG_CHECKING(Optimization flags)
AC_ARG_WITH(target-optflags,
[  --with-target-optflags    Optimization flags to use (Default -O2)],
target_optflags="${withval}",
target_optflags="-O2")
AC_MSG_RESULT(${target_optflags} )

AC_MSG_CHECKING([whether the assembler needs -Wa to forward options])

# Write out a minimal assembler file
cat > conftest.s << 'EOF'
    .globl  foo
foo:
    nop
EOF

# Try compiling using the assembler explicitly
if $AS $target_asflags -Wa,--defsym,foo=1 -c conftest.s -o conftest.o >/dev/null 2>&1; then
  AC_MSG_RESULT([yes])
  AC_DEFINE([HAVE_AS_DEF_SYM], [1], [Assembler needs -Wa])
  ASFWDOPT="-Wa,"
  ASDEFSYM="${ASFWDOPT}--defsym,"
else
  AC_MSG_RESULT([no])
  ASDEFSYM="--defsym "
fi

rm -f conftest.s conftest.o

case $host in
 i?86be-pc-amithlon)
  : ${CPU="i686"}
  cflags="$target_cflags -noixemul"
  optflags="-mcpu=${CPU} $target_optflags"
  linkerflags="$target_ldflags -noixemul -mcpu=${CPU} -r"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU} ${ASDEFSYM}LITTLE_ENDIAN=1"
  resident=""                                      dnl *** Not supported (yet?)
  stripflag=""
 ;;
 i?86-*-aros)
 : ${CPU="i486"}
  cflags="$target_cflags "
  optflags="-m32 -march=${CPU} $target_optflags"
  linkerflags="$target_ldflags"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU} ${ASDEFSYM}LITTLE_ENDIAN=1"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 m68k-unknown-amigaos)
  : ${CPU="68020-60"}
  cflags="$target_cflags -noixemul"
  optflags="-m${CPU} $target_optflags -fomit-frame-pointer -ffast-math -msoft-float"
  linkerflags="$target_ldflags -noixemul -m${CPU}"
  asCPU=`echo ${CPU} | sed 's/-.*//'`
  asflags="$target_asflags -m${asCPU} ${ASFWDOPT}--register-prefix-optional ${ASFWDOPT}--bitwise-or ${ASDEFSYM}CPU=${asCPU}"
  resident="-resident"
  buildhandler="true"
  stripflag=""
 ;;
 m68k-*-aros)
  : ${CPU="68020-60"}
  cflags="$target_cflags "
  optflags="-m${CPU} $target_optflags -fomit-frame-pointer -ffast-math -msoft-float"
  linkerflags="$target_ldflags -m${CPU}"
  asCPU=`echo ${CPU} | sed 's/-.*//'`
  asflags="$target_asflags -m${asCPU} ${ASFWDOPT}--register-prefix-optional ${ASFWDOPT}--bitwise-or ${ASDEFSYM}CPU=${asCPU}"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 powerpc-unknown-amigaos)
  : ${CPU="603"}
  AC_DEFINE([__USE_INLINE__], 1, [Use inlines])
  AC_DEFINE([__USE_BASETYPE__], 1, [Use traditional library base type])
  cflags="$target_cflags "
  optflags="-mcpu=${CPU} $target_optflags"
  linkerflags="$target_ldflags -mcpu=${CPU}"
  asflags="$target_asflags -m${CPU} -mregnames ${ASDEFSYM}CPU=${CPU}"
  resident=""                                      dnl *** Not supported (yet?)
  stripflag=""
 ;;
 powerpc-unknown-morphos)
  : ${CPU="603"}
  AC_DEFINE([USE_INLINE_STDARG], 1, [Use inlines for stdarg functions])
  cflags="$target_cflags -noixemul"
  optflags="-mcpu=${CPU} $target_optflags"
  linkerflags="$target_ldflags -noixemul -mcpu=${CPU}"
  asflags="$target_asflags -m${CPU} -mregnames ${ASDEFSYM}CPU=${CPU}"
  resident="-mresident32"
  buildhandler="true"
  stripflag=""
 ;;
 x86_64-*-aros)
 : ${CPU="x86_64"}
  cflags="$target_cflags "
  optflags="$target_optflags -fomit-frame-pointer"
  linkerflags="$target_ldflags"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU} ${ASDEFSYM}LITTLE_ENDIAN=1 ${ASDEFSYM}x86_64=1"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 aarch64*-*-aros)
 : ${CPU="aarch64"}
  cflags="$target_cflags "
  optflags="$target_optflags -fomit-frame-pointer"
  linkerflags="$target_ldflags"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU} ${ASDEFSYM}LITTLE_ENDIAN=1"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 arm*-*-aros)
 : ${CPU="arm"}
  cflags="$target_cflags "
  optflags="$target_optflags -fomit-frame-pointer"
  linkerflags="$target_ldflags"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU} ${ASDEFSYM}LITTLE_ENDIAN=1"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 powerpc*-*-aros)
 : ${CPU="440fp"}
  cflags="$target_cflags "
  optflags="$target_optflags -fomit-frame-pointer"
  linkerflags="$target_ldflags"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU}"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 riscv*-*-aros)
 : ${CPU="arm"}
  cflags="$target_cflags "
  optflags="$target_optflags -fomit-frame-pointer"
  linkerflags="$target_ldflags"
  asflags="$target_asflags ${ASDEFSYM}CPU=${CPU} ${ASDEFSYM}LITTLE_ENDIAN=1"
  resident=""
  buildhandler="true"
  stripflag=""
 ;;
 *)
  AC_MSG_WARN(Unknown architecture!)
 ;;
esac

top_builddir=$(pwd)

: ${CFLAGS="-g -W -Wall -Wstrict-prototypes -Wno-unused ${cflags} ${optflags}"}
: ${CPPFLAGS="-DCPU=\"\\\"${CPU}\\\"\" -I. -I${srcdir} -I${top_builddir} -I${top_builddir}/Include/gcc ${os_includedir} $target_cppflags"}
: ${LDFLAGS="${linkerflags}"}
: ${ASFLAGS="${asflags}"}
: ${ARFLAGS="-ru"}
: ${STRIPFLAGS="${stripflag}"}

: ${CFLAG_RESIDENT="${resident}"}
: ${LDFLAG_RESIDENT="${resident}"}

: ${DISTDIR="/tmp/${host_alias}-ahi"}
: ${AHI_BUILDHANDLER="${buildhandler}"}

AC_PREFIX_DEFAULT( / )

AC_SUBST(ASFLAGS)
AC_SUBST(ARFLAGS)
AC_SUBST(STRIPFLAGS)
AC_SUBST(CFLAG_RESIDENT)
AC_SUBST(LDFLAG_RESIDENT)
AC_SUBST(DISTDIR)

AC_PROG_INSTALL
#AC_PROG_CC
AC_PROG_RANLIB
AC_CHECK_PROG( RM, rm, rm )
AC_CHECK_PROGS( ROBODOC, RoboDoc robodoc )
AC_CHECK_PROGS( FLEXCAT, FlexCat flexcat )
AC_CHECK_PROGS( SFDC, SFDC sfdc )
AC_CHECK_PROGS( LHA, lha )

if test -z "$SFDC"; then
 AC_MSG_ERROR( sfdc missing. )
fi

if test -z "$LHA"; then
 AC_MSG_WARN( lha missing. )
else
 if test -n "`$LHA 2>&1 | grep 'o.*compression method'`"; then
  LHA="$LHA -ao5"
 else
  LHA="$LHA -a -r"
 fi
fi

if test -z "$ROBODOC"; then
 AC_MSG_WARN( robodoc missing. )
fi

dnl Checks for libraries.

AC_SEARCH_LIBS([NewList],      [amiga],, [AC_MSG_WARN(libamiga is missing.)])
AC_SEARCH_LIBS([floor],        [m],,	 [AC_MSG_ERROR(libm is missing.)])
AC_SEARCH_LIBS([IntuitionBase],[auto],,  [AC_MSG_WARN(Who cares?)])
AC_SEARCH_LIBS([LayoutBase],   [raauto],,[AC_MSG_WARN(Who cares?)])
AC_SEARCH_LIBS([MUI_NewObject],[mui],[AC_DEFINE([HAVE_LIBMUI], [1], [Define to 1 if you have the `mui' library (-lmui)])],   [AC_MSG_WARN(Who cares?)])
AC_SUBST(HAVE_LIBMUI)

dnl Checks for library functions.

case $host in
 i?86be-pc-amithlon)
  : ${CPU="i686"}
  LIBS="-lm $LIBS" dnl *** AC_CHECK_LIBS doesn't work with -r ...?
 ;;
esac

AC_SUBST(AHI_BUILDHANDLER)

dnl Checks for native driver support header files.

AC_CHECK_HEADERS( asm/io.h, HAVE_ASMIO=1, AC_MSG_WARN( "asm/io.h is missing." ))
AC_CHECK_HEADERS( libraries/openpci.h, HAVE_OPENPCI=1, AC_MSG_WARN( "libraries/openpci.h is missing." ))
AC_SUBST(HAVE_ASMIO)
AC_SUBST(HAVE_OPENPCI)

dnl Checks for AROS hosted driver header files.

AC_CHECK_HEADERS( proto/oss.h, [ AC_MSG_RESULT(Hosted OSS Driver will be built); HAVE_OSS=1],)
AC_SUBST(HAVE_OSS)

dnl Check for AROS hosted driver support linklibs

AC_CHECK_LIB(alsa-bridge, ALSA_Init,  [ AC_MSG_RESULT(Hosted Alsa Driver will be built); HAVE_ALSA=1],)
AC_CHECK_LIB(pulseaudio-bridge, PULSEA_Init,  [ AC_MSG_RESULT(Hosted PulseAudio Driver will be built); HAVE_PULSEAUDIO=1],)
AC_CHECK_LIB(WASAPI-bridge, WASAPI_Init,  [ AC_MSG_RESULT(Hosted WASAPI Driver will be built); HAVE_WASAPI=1],)
AC_SUBST(HAVE_ALSA)
AC_SUBST(HAVE_PULSEAUDIO)
AC_SUBST(HAVE_WASAPI)

dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN

dnl

AC_OUTPUT(Makefile
          AHI/Makefile
          AHI-Handler/Makefile
          AddAudioModes/Makefile
          Device/Makefile
          Docs/Makefile
          Drivers/Makefile
          Drivers/Common/Makefile.common
          Drivers/Device/Makefile
          Drivers/Filesave/Makefile
          Drivers/Void/Makefile
          Drivers/ac97/Makefile
          Drivers/VIA-AC97/Makefile
          Drivers/HDAudio/Makefile
          Drivers/CMI8738/Makefile
          Drivers/SB128/Makefile
          Drivers/EMU10kx/Makefile
          Drivers/Envy24HT/Makefile
          Drivers/Envy24/Makefile
          Drivers/Alsa/Makefile
          Drivers/OSS/Makefile
          Drivers/PulseAudio/Makefile
          Drivers/WASAPI/Makefile
          Examples/Makefile
          Examples/Device/PlayTest/Makefile
          Examples/Extras/CloneAudioModes/Makefile
          Examples/Low-level/PlaySineEverywhere/Makefile
          Examples/Low-level/Test-7.1/Makefile
          Examples/Low-level/ScanAudioModes/Makefile
          Include/Makefile,
          echo timestamp > stamp-h)
