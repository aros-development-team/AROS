SetClock LOAD

FailAt 21

Makedir RAM:Clipboards RAM:ENV RAM:ENV/SYS
If NOT EXISTS RAM:T
    Makedir RAM:T
EndIf

Assign ENV:     RAM:ENV
Assign T:       RAM:T
Assign CLIPS:   RAM:Clipboards

Assign KEYMAPS: DEVS:Keymaps
Assign LOCALE:  SYS:Locale
Assign LIBS:    SYS:Classes ADD
Assign FONTS:   SYS:Fonts
Assign >NIL: Fonts: SYS:Fonts/Russian ADD
Assign HELP:    LOCALE:Help DEFER
Assign THEMES:  SYS:System/Themes >NIL:
Assign IMAGES:  SYS:System/Images DEFER
;Assign PRINTERS: DEVS:Printers
Assign WANDERER: SYS:System/Wanderer DEFER

Mount >NIL: DEVS:DOSDrivers/~(#?.info)

If EXISTS SYS:Classes/USB
    Assign USBCLASSES: SYS:Classes/USB
EndIf

If EXISTS SYS:DiskImages
    Assign FDSK: SYS:DiskImages
EndIf

Touch PIPEFS:systempipe
Assign PIPE:    PIPEFS:systempipe

Path C: SYS:System S: SYS:Prefs SYS:Tools SYS:Utilities QUIET

If EXISTS SYS:Tools/Commodities
    Path SYS:Tools/Commodities QUIET ADD
EndIf

If EXISTS SYS:Demos
    Path SYS:Demos ADD QUIET
EndIf

If EXISTS Sys:Rexxc
    Path Sys:Rexxc ADD QUIET
Endif

Copy >NIL: ENVARC: ENV: ALL NOPRO NOREQ

Assign THEME: $SYS/theme.var

Touch >NIL: FONTS:__TEST__
If WARN
    Makedir RAM:Fonts
    Assign FONTS: RAM:Fonts
    Copy >NIL: SYS:Fonts FONTS: ALL CLONE NOREQ
Endif

IF EXISTS FONTS:__TEST__
    Delete FONTS:__TEST__ QUIET
Endif

If NOT EXISTS FONTS:fixed.font
    FixFonts
EndIf

AddDatatypes REFRESH QUIET
IPrefs
Run <NIL: >NIL: ConClip

If EXISTS C:RexxMast
    Assign REXX: S:
    Run <NIL: >NIL: C:RexxMast
EndIf

If EXISTS C:Decoration
    C:Decoration <NIL: >NIL: 
Endif

Assign EXISTS Extras: >NIL:
If WARN
    If Exists $EXTRASPATH
        Assign Extras: $EXTRASPATH
    EndIf
Endif
Assign EXISTS Extras: >NIL:
If WARN
    Assign Extras: SYS:Extras >NIL:
EndIf

If EXISTS ENV:SYS/Packages
    List ENV:SYS/Packages NOHEAD FILES TO T:P LFORMAT="If EXISTS $SYS/Packages/%s*NCD $SYS/Packages/%s*NIf EXISTS S/Package-Startup*NExecute S/Package-Startup*NEndif*NEndif*N"
    Execute T:P
    Delete T:P QUIET
    CD SYS:
EndIf

If EXISTS S:User-Startup
    Execute S:User-Startup
EndIf

WANDERER:Wanderer

If NOT ERROR
    EndCLI
EndIf
