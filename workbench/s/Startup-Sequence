SetClock LOAD

Mount PIPEFS:

FailAt 21

Makedir RAM:Clipboards RAM:ENV RAM:ENV/SYS
If NOT EXISTS RAM:T
    Makedir RAM:T
EndIf

Assign ENV:     RAM:ENV
Assign T:       RAM:T
Assign CLIPS:   RAM:Clipboards

Assign KEYMAPS: DEVS:Keymaps
Assign LOCALE:  SYS:Locale
Assign LIBS:    SYS:Classes ADD
Assign FONTS:   SYS:Fonts
Assign >NIL: Fonts: SYS:Fonts/Russian ADD
Assign HELP:    LOCALE:Help DEFER
Assign THEMES:  SYS:System/Themes
Assign THEME:   THEMES:Mason
Assign IMAGES:  SYS:System/Images DEFER
;Assign PRINTERS: DEVS:Printers
Assign WANDERER: SYS:System/Wanderer DEFER

If EXISTS SYS:DiskImages
    Assign FDSK: SYS:DiskImages
EndIf

Touch PIPEFS:systempipe
Assign PIPE:    PIPEFS:systempipe

Path C: SYS:System S: SYS:Prefs SYS:Tools SYS:Utilities QUIET

If EXISTS SYS:Tools/Commodities
    Path SYS:Tools/Commodities QUIET ADD
EndIf

If EXISTS SYS:Demos
    Path SYS:Demos ADD QUIET
EndIf

Copy >NIL: ENVARC: ENV: ALL CLONE NOREQ

Touch >NIL: FONTS:__TEST__
If WARN
    Makedir RAM:Fonts
    Assign FONTS: RAM:Fonts
    Copy >NIL: SYS:Fonts FONTS: ALL CLONE NOREQ
Endif

IF EXISTS FONTS:__TEST__
    Delete FONTS:__TEST__ QUIET
Endif

If NOT EXISTS FONTS:fixed.font
    FixFonts
EndIf

AddDatatypes REFRESH QUIET
IPrefs
Run <NIL: >NIL: ConClip

If EXISTS C:RexxMast
    Assign REXX: S:
    Run <NIL: >NIL: C:RexxMast
EndIf

If EXISTS ENV:SYS/Packages
    List ENV:SYS/Packages NOHEAD FILES TO T:P LFORMAT="If EXISTS $SYS/Packages/%s*NCD $SYS/Packages/%s*NIf EXISTS S/Package-Startup*NExecute S/Package-Startup*NEndif*NEndif*N"
    Execute T:P
    Delete T:P QUIET
    CD SYS:
EndIf

If EXISTS S:User-Startup
    Execute S:User-Startup
EndIf

WANDERER:Wanderer

If NOT ERROR
    EndCLI
EndIf
