boot/AROSBootstrap ROM boot/aros.hunk.gz
SetClock LOAD

FailAt 21

Makedir RAM:Clipboards
If NOT EXISTS RAM:T
    Makedir RAM:T
EndIf
If NOT EXISTS RAM:ENV
    Makedir RAM:ENV
    Assign ENV: RAM:ENV
EndIf

Assign T:       RAM:T
Assign CLIPS:   RAM:Clipboards

Assign KEYMAPS: DEVS:Keymaps
Assign LOCALE:  SYS:Locale
Assign LIBS:    SYS:Classes ADD
Assign FONTS:   SYS:Fonts
Assign HELP:    LOCALE:Help DEFER
Assign THEMES:  SYS:System/Themes >NIL:
Assign IMAGES:  SYS:System/Images DEFER
;Assign PRINTERS: DEVS:Printers
Assign WANDERER: SYS:System/Wanderer DEFER

If EXISTS SYS:Classes/USB
    Assign USBCLASSES: SYS:Classes/USB
    AddUSBClasses >NIL:
EndIf

If EXISTS SYS:DiskImages
    Assign FDSK: SYS:DiskImages
EndIf

Mount DEVS:DOSDrivers/~(#?.info|PIPEFS|XPIPE)

Path C: SYS:System S: SYS:Prefs SYS:Tools SYS:Utilities QUIET

If EXISTS SYS:Tools/Commodities
    Path SYS:Tools/Commodities QUIET ADD
EndIf

If EXISTS SYS:Demos
    Path SYS:Demos ADD QUIET
EndIf

If EXISTS Sys:Rexxc
    Path Sys:Rexxc ADD QUIET
Endif

Copy >NIL: ENVARC: ENV: ALL NOPRO NOREQ PAT ~(def_#?.info)

If EXISTS ENV:SYS/theme.var
    Assign THEME: ${SYS/theme.var}
else
    Assign THEME: THEMES:OS3.1/
endif

Touch >NIL: FONTS:__TEST__
If WARN
    Makedir RAM:Fonts
    Assign FONTS: RAM:Fonts
    Copy >NIL: SYS:Fonts FONTS: ALL CLONE NOREQ
Endif

Assign >NIL: FONTS: FONTS:Russian ADD

IF EXISTS FONTS:__TEST__
    Delete FONTS:__TEST__ QUIET
Endif

AddDatatypes REFRESH QUIET
IPrefs
Run >NIL: QUIET ConClip

If EXISTS C:RexxMast
    Assign REXX: S:
    Run >NIL: QUIET C:RexxMast
EndIf

Assign EXISTS Extras: >NIL:
If WARN
    If Exists $EXTRASPATH
        Assign Extras: $EXTRASPATH
    Else
        Assign Extras: SYS:Extras >NIL:
        If WARN
            Assign Extras: NIL: >NIL:
        EndIf
    EndIf
Endif

Assign EXISTS Development: >NIL:
If WARN
    If Exists $DEVELPATH
        Assign Development: $DEVELPATH
    EndIf
Endif
Assign EXISTS Development: >NIL:
If WARN
    Assign Development: SYS:Development >NIL:
EndIf

If EXISTS ENV:SYS/Packages
    List ENV:SYS/Packages NOHEAD FILES TO T:P LFORMAT="If EXISTS ${SYS/Packages/%N}*NCD ${SYS/Packages/%N}*NIf EXISTS S/Package-Startup*NExecute S/Package-Startup*NEndif*NEndif*N"
    Execute T:P
    Delete T:P QUIET
    CD SYS:
EndIf

If EXISTS S:User-Startup
    Execute S:User-Startup
EndIf

WANDERER:Wanderer

If NOT ERROR
    EndCLI
EndIf
